10 CLS
20 PRINT "OS MELHORES JOGOS PARA FX-880P  " CHR$(145)" RULETA RUSSA               " CHR$(227) CHR$(229);
30 OPTION$=INPUT$(1,@)
35 IF ASC(OPTION$)=31 THEN GOTO 60
40 IF ASC(OPTION$)=49 THEN GOTO 3000 ELSE GOTO 10
60 CLS
70 PRINT CHR$(146) " MEMORY                      " CHR$(227) CHR$(229) CHR$(147)" MASTER MIND";
75 OPTION$=INPUT$(1,@)
80 IF ASC(OPTION$)=31 THEN GOTO 90
83 IF ASC(OPTION$)=30 THEN GOTO 10
85 IF ASC(OPTION$)=50 THEN GOTO 4000
87 IF ASC(OPTION$)=51 THEN GOTO 5000 ELSE GOTO 60
90 CLS
100 PRINT CHR$(148) " GOLF                        " CHR$(227) CHR$(229) CHR$(149)" ENFORCADO";
105 OPTION$=INPUT$(1,@)
110 IF ASC(OPTION$)=30 THEN GOTO 60
115 IF ASC(OPTION$)=52 THEN GOTO 6000
117 IF ASC(OPTION$)=53 THEN GOTO 8000 ELSE GOTO 90
128 LOCATE I,1:PRINT " o";
3000 REM RULETA RUSSA
3010 CLS:CLEAR:DIM a$(3)
3030 CLS:BEEP 0
3040 p$="STOP:EXE    **¥i¥i¥i¥**"
3050 PRINT p$;
3060 FOR n=1 TO 3
3070 r=FIX(RAN#*3)+FIX(RAN#*2)
3080 a$(n)=CHR$(232+r)
3090 LOCATE 13+(n*2),0
3100 PRINT a$(n);
3110 k$=INKEY$
3120 IF k$=CHR$(13) GOTO 3150
3130 r=r+1:IF r=4 THEN r=0
3140 GOTO 3080
3150 BEEP1
3160 FOR x=1 TO 20:NEXT x
3170 NEXT n
3180 LOCATE 2,1
3190 IF a$(1)=a$(2) AND a$(2)=a$(3) AND a$(3)=a$(1) GOTO 3250
3200 IF a$(1)=a$(2) OR a$(2)=a$(3) OR a$(3)=a$(1) GOTO 3230
3210 PRINT "Ah... lentolas";
3220 GOTO 3260
3230 PRINT "Quase, quase";
3240 GOTO 3260
3250 PRINT "Que maquina es";
3260 PRINT " (Prime EXE)"
3270 GOTO 3030
4000 REM MEMORY v2.0
4010 ERASE ARRAY1, ARRAY2:DIM ARRAY1(14,1), ARRAY2(14,1)
4020 TENT=0:ABRE=0:CONT=0
4030 CLS:PRINT "ESCOLHE NIVEL:                  1)FACIL 2)DIFICIL             ";
4040 OP$=INPUT$(1,@)
4045 IF OP$="1" THEN NIV=64:GOTO 4050
4046 IF OP$="2" THEN NIV=166 ELSE GOTO 4030
4050 CLS:PRINT "CALCULANDO... ";
4100 FOR I=0 TO 14:FOR J=0 TO 1
4110 A=INT(RAN#*15):B=INT(RAN#*2):IF ARRAY1(A,B)=0 THEN ARRAY1(A,B)=NIV+I ELSE GOTO 4110
4120 NEXT J
4130 PRINT CHR$(134);
4140 NEXT I
4150 REM --- GRAFICO ---
4160 CLS:FOR J=0 TO 1:FOR I=0 TO 14:LOCATE I*2+1,J:PRINT CHR$(135);
4170 NEXT I,J
4175 REM --- CURSOR ---
4200 X=0:Y=0:XX=0:YY=0
4205 REM --- OPCAO ---
4210 LOCATE X*2,Y:PRINT "[";:LOCATE X*2+2,Y:PRINT "]";:OP$=INKEY$
4220 IF OP$="" GOTO 4210
4230 IF OP$=CHR$(31) AND Y=0 THEN Y=Y+1:GOTO 4300
4240 IF OP$=CHR$(30) AND Y=1 THEN Y=Y-1:GOTO 4300
4250 IF OP$=CHR$(29) AND X>0 THEN X=X-1:GOTO 4300
4260 IF OP$=CHR$(28) AND X<14 THEN X=X+1:GOTO 4300
4270 IF OP$=" " AND ABRE=0 THEN GOTO 4400
4280 IF OP$=" " AND ABRE=1 THEN GOTO 4440
4290 GOTO 4210
4295 REM --- LIMPAR ---
4300 LOCATE XX*2,YY:PRINT " ";:LOCATE XX*2+2,YY:PRINT " ";:XX=X:YY=Y:GOTO 4210
4400 LOCATE X*2+1,Y:PRINT CHR$(ARRAY1(X,Y));
4410 ABRE=1:XA=X:YA=Y
4420 GOTO 4210
4440 IF X=XA AND Y=YA THEN GOTO 4210
4445 LOCATE X*2+1,Y:PRINT CHR$(ARRAY1(X,Y));:ABRE=0
4450 TENT=TENT+1:Q=ARRAY1(X,Y):W=ARRAY1(XA,YA):IF Q=W THEN GOTO 4600 ELSE GOTO 4500
4500 FOR I=1 TO 100:NEXT
4510 LOCATE X*2+1,Y:PRINT CHR$(135);
4520 LOCATE XA*2+1,YA:PRINT CHR$(135);:GOTO 4210
4600 CONT=CONT+1
4620 IF CONT=15 THEN CLS ELSE GOTO 4210
4630 PRINT "PARABENS, GANHAS-TE COM";TENT;" TENTATIVAS!"
4640 RETURN
5000 REM MASTERMIND
5010 CLS:NPOS=5
5020 NCHIF=8
5030 DIM TIR(6),PROP(6),TIRBIS(6)
5040 NCOUP=0
5041 REM ----- SORTEIO DA COMBINACAO GANHADORA -----
5050 FOR I=1 TO NPOS
5060 TIR(I)=INT(NCHIF*RAN#+1)
5070 NEXT I
5080 GOSUB 5140
5090 GOSUB 5200
5100 NCOUP=NCOUP+1
5110 IF BPOS<NPOS THEN GOTO 5080
5111 REM ----- FIM -----
5120 PRINT
5130 CLS:BEEP1:BEEP1:PRINT "* GANHAS-TE COM"NCOUP "TENTATIVAS *":GOTO 10
5131 REM ----- SUB ROTINA:ENTRADA DA PROPOSTA EM PROP(NPOS) -----
5140 CLS:LOCATE 4,0:PRINT "INTRODUZ 5 NUMEROS ";:INPUT@5;CH$
5150 IF LEN(CH$)<>NPOS THEN BEEP:BEEP:PRINT "**INCORRECTO INTRODUZ 5 LETRAS**":GOTO 5140
5160 FOR I=1 TO NPOS
5170 PROP(I)=VAL(MID$(CH$,I,1))
5180 NEXT I
5190 RETURN
5191 REM ----- SUB ROTINA:ANALISE DA PROPOSTA -----
5192 REM -----
5193 REM -----
5194 REM ----- DUPLICACAO DO SORTEIO -----
5200 FOR I=1 TO NPOS
5210 TIRBIS(I)=TIR(I)
5220 NEXT I
5221 REM ----- CONTA DAS BOAS PROPOSTAS
5230 BPOS=0
5240 FOR I=1 TO NPOS
5250 IF PROP(I)=TIRBIS(I) THEN BPOS=BPOS+1:TIRBIS(I)=0:PROP(I)=0
5260 NEXT I
5261 REM ----- CONTA DOS BONS NUMEROS MAL COLOCADOS -----
5270 BCHIF=0
5280 FOR I=1 TO NPOS
5290 FOR J=1 TO NPOS
5300 IF PROP(I)=0 THEN 5320
5310 IF PROP(I)=TIRBIS(J) THEN BCHIF=BCHIF+1:PROP(I)=0:TIRBIS(J)=0
5320 NEXT J
5330 NEXT I
5331 REM ----- APRESENTACAO DOS RESULTADOS -----
5340 BEEP:PRINT TAB(3) STR$(BPOS)" COLOCADOS "STR$(BCHIF)" CORRECTOS"
5350 RETURN
6000 REM JOGO DE GOLF
6001 CLS:G=0:N=50
6020 PRINT "JOGO DE GOLF";:GOSUB 6900:PRINT "BEST-SCORE";N;:GOSUB 6900
6030 DIM Q(10):Q(1)=4:Q(2)=5:Q(3)=4:Q(4)=3:Q(5)=4:Q(6)=5:Q(7)=4:Q(8)=3:Q(9)=4
6050 FOR H=1 TO 9
6060 L=Q(H)*20-20+INT(RAN#*10)
6070 PRINT H":PAS"Q(H)L"M";:GOSUB 6900:GOSUB 6900:GOSUB 6900
6080 GOSUB 6120:G=G+C:PRINT "("C-Q(H)")";:GOSUB 6900:GOSUB 6900
6090 NEXT H:IF G<N;N=G:PRINT "** BEST="G"**":G=0:GOTO 10
6100 PRINT "SCORE="G
6110 G=0:GOTO 10
6120 C=0
6130 CLS:LOCATE 0,0:PRINT L;" PRIME UMA TECLA";
6140 IF INKEY$<>""THEN 6140
6150 IF INKEY$="" THEN 6150
6155 GOSUB 6900
6160 CLS:LOCATE 0,0:PRINT L;"SHOT!";:C=C+1:FOR I=1 TO 1E99:IF KEY$=""THEN 6200
6170 NEXT I
6200 J=SGN L
6210 FOR B=1 TO I:L=L-J:LOCATE 0,0:PRINT :CLS:LOCATE 0,0:PRINT L;:NEXT B
6220 IF L=0;RETURN
6230 GOTO 6140
6900 FOR I=1 TO 200:NEXT I
6920 PRINT
6930 RETURN
8000 REM O ENFORCADO
8010 CLEAR :CLS
8020 GOSUB 8210
8030 CLS:INPUT "PALAVRA";P$
8040 A=LEN(P$):IF A>30 OR A<1 THEN GOTO 8030
8050 DIM F$(30)
8060 FOR I=1 TO A
8070 F$(I)=MID(P$,I,1):NEXT I
8080 CLS:PRINT "(------------------------------";:LOCATE A+1,0:PRINT ")                            ";
8090 LOCATE 0,1:PRINT "[********]";8;
8100 C=0:LOCATE 14,1:P2$=INPUT$(1)
8110 FOR I=1 TO A
8120 IF P2$=F$(I) THEN LOCATE I,0:PRINT F$(I);:D=D+1:C=1:F$(I)=""
8130 NEXT I
8140 IF C=1 THEN 8170
8150 B=B+1:LOCATE B,1:PRINT P2$;:LOCATE 10,1:PRINT 8-B;
8160 IF B=8 THEN 8190 ELSE 8100
8170 IF A=D THEN 8180 ELSE 8100
8180 LOCATE 15,1:PRINT "GANHAS-TE !!!":GOTO 8010
8190 LOCATE 1,0:PRINT P$;:LOCATE 15,1:PRINT "ENFORCARAM-TE!!!";
8200 LOCATE A+3,0:PRINT CHR$(228);CHR$(228);"CORRECTA":GOTO 8010
8210 REM APRESENTACAO
8220 FOR I=1 TO 12
8230 LOCATE 31-I,0:PRINT "o ";
8240 LOCATE I,0:PRINT " E";
8250 NEXT I
8260 FOR T=1 TO 200:NEXT T
8270 FOR I=1 TO 14
8280 PRINT CHR$(30);
8290 LOCATE 31-I,1:PRINT "a ";
8310 NEXT I
8320 FOR T=1 TO 200:NEXT T
8330 LOCATE 32-I,1:PRINT " ";
8340 LOCATE 32-I,0:PRINT "a";
8350 LOCATE I,1:PRINT " ";
8360 LOCATE I,0:PRINT "o";
8370 FOR I=30 TO 13 STEP -1
8380 PRINT CHR$(30);
8390 LOCATE I+1,1:PRINT "f ";
8400 LOCATE 30-I,1:PRINT " d";
8410 NEXT I
8420 FOR T=1 TO 200:NEXT T
8430 LOCATE I+2,1:PRINT " ";
8440 LOCATE I+2,0:PRINT "f";
8450 FOR T=1 TO 200:NEXT T
8460 LOCATE 30-I,1:PRINT " ";
8470 LOCATE 30-I,0:PRINT "d";
8480 FOR I=1 TO 11
8490 IF I/2=INT(I/2) THEN Y=0:X=1:GOTO 8520
8500 Y=1:X=0
8510 PRINT CHR$(30);
8520 LOCATE I,Y:PRINT "c";
8530 LOCATE I-1,X:PRINT " ";
8540 NEXT I
8550 FOR I=11 TO 15
8560 LOCATE I,1:PRINT " c";
8570 FOR T=1 TO 5:NEXT T
8580 NEXT I
8590 LOCATE I,1:PRINT "  ";
8600 LOCATE I,0:PRINT "c";
8610 FOR I=30 TO 10 STEP -2
8620 PRINT CHR$(30);
8630 LOCATE I,1:PRINT "r  ";
8640 NEXT I
8650 FOR I=1 TO 16
8660 PRINT CHR$(30);
8670 LOCATE I,1:PRINT " r";
8680 NEXT I
8690 FOR I=17 TO 14 STEP -1
8700 PRINT CHR$(30);
8710 LOCATE I,1:PRINT "r ";
8720 NEXT I
8730 FOR T=1 TO 100:NEXT T
8740 LOCATE 14,1:PRINT " r";
8750 FOR T=1 TO 100:NEXT T
8760 LOCATE 13,0:PRINT "   ";:LOCATE 12,0:PRINT "Efo";
8770 FOR T=1 TO 200:NEXT T
8780 I=15
8790 LOCATE I,1:PRINT " ";
8800 LOCATE I,0:PRINT "r";
8820 LOCATE 11,1:PRINT "by atuPauS";
8830 FOR I=0 TO 3
8840 FOR T=1 TO 20:NEXT T
8850 NEXT I
8860 FOR T=1 TO 20:NEXT T
8870 FOR T=1 TO 20:NEXT T
8880 STOP
8890 RETURN
